module String.EditDistance exposing
    ( calculate
    , findMostSimilar
    )

{-| Functions for working with [Levenshtein distance](https://en.wikipedia.org/wiki/Levenshtein_distance) on `String`s.

The Levenshtein distance can be used as a metric for how similar two `String`s are. We can use this to suggest alternatives
when the user has given us a value we don't recognize.

Based on code from wikipedia article on [Levenshtein distance](https://en.wikipedia.org/wiki/Levenshtein_distance)

@docs calculate, findMostSimilar
-}

--TODO: deletion/insertion should only add a distance of 0.5
--TODO: add support for transposition
--TODO: performance?


{-| Calculate the Levenshtein distance between two `String`s.
-}
calculate : String -> String -> Int
calculate left right =
    when String.popFirst left is
        Nothing ->
            String.count right

        Just { first = firstLeft, rest = restLeft } ->
            when String.popFirst right is
                Nothing ->
                    String.count left

                Just { first = firstRight, rest = restRight } ->
                    if firstLeft == firstRight then
                        calculate restLeft restRight

                    else
                        1 +
                            ([ calculate left restRight -- character inserted
                            , calculate restLeft right -- character deleted
                            , calculate restLeft restRight -- character replaced
                            ]
                                |> Array.minimum
                                |> Maybe.withDefault 0
                            )


{-| Given an `Array` of `String`, find the value that is most similar to the given `String`.
-}
findMostSimilar : { maxAmount : Int, maxDistance : Int, from : Array String } -> String -> Array String
findMostSimilar { maxAmount, maxDistance, from } str =
    let
        byDistance =
            from
                |> Array.map (\sample -> { distance = calculate sample str, sample = sample })
                |> Array.keepIf (\{ distance } -> distance <= maxDistance)

        minDistance =
            byDistance
                |> Array.map .distance
                |> Array.minimum
                |> Maybe.withDefault 0
    in
    byDistance
        |> Array.keepIf (\{ distance } -> distance == minDistance)
        |> Array.takeFirst maxAmount
        |> Array.map .sample
