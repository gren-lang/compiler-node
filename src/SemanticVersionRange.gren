module SemanticVersionRange exposing
    ( SemanticVersionRange
    , of
    , example
    , lowerBound
    , upperBound
    , intersect
    , compatibleWith
    , fromExact
    , fromString
    , jsonDecoder
    , toString
    , toJson
    )


import SemanticVersion exposing (SemanticVersion)
import Json.Decode as Decode exposing (Decoder)
import Json.Encode as Encode


type SemanticVersionRange
    = SemanticVersionRange { lower : SemanticVersion, upper : SemanticVersion }


of : SemanticVersion -> SemanticVersion -> Maybe SemanticVersionRange
of lower upper =
    if SemanticVersion.compare lower upper /= GT then
        Just (SemanticVersionRange { lower = lower, upper = upper })

    else
        Nothing


example : SemanticVersionRange
example =
    SemanticVersionRange
        { lower = { major = 1, minor = 0, patch = 0 }
        , upper = { major = 2, minor = 0, patch = 0 }
        }


lowerBound : SemanticVersionRange -> SemanticVersion
lowerBound (SemanticVersionRange { lower }) =
    lower


upperBound : SemanticVersionRange -> SemanticVersion
upperBound (SemanticVersionRange { upper }) =
    upper


intersect : SemanticVersionRange -> SemanticVersionRange -> Maybe SemanticVersionRange
intersect left right =
    let
        (SemanticVersionRange { lower = lowerLeft, upper = upperLeft }) =
            left

        (SemanticVersionRange { lower = lowerRight, upper = upperRight }) =
            right
    in
    when SemanticVersion.compare upperLeft upperRight is
        LT ->
            if SemanticVersion.compare upperLeft lowerRight /= GT then
                Nothing

            else
                Just <|
                    when SemanticVersion.compare lowerLeft lowerRight is
                        LT ->
                            SemanticVersionRange { lower = lowerRight, upper = upperLeft }

                        EQ ->
                            SemanticVersionRange { lower = lowerLeft, upper = upperLeft }

                        GT ->
                            SemanticVersionRange { lower = lowerLeft, upper = upperLeft }

        EQ ->
            Just <|
                when SemanticVersion.compare lowerLeft lowerRight is
                    LT ->
                        SemanticVersionRange { lower = lowerRight, upper = upperLeft }

                    EQ ->
                        SemanticVersionRange { lower = lowerLeft, upper = upperLeft }

                    GT ->
                        SemanticVersionRange { lower = lowerLeft, upper = upperLeft }

        GT ->
            if SemanticVersion.compare upperRight lowerLeft /= GT then
                Nothing
            else
                Just <|
                    when SemanticVersion.compare lowerLeft lowerRight is
                        LT ->
                            SemanticVersionRange { lower = lowerLeft, upper = upperRight }

                        EQ ->
                            SemanticVersionRange { lower = lowerLeft, upper = upperRight }

                        GT ->
                            SemanticVersionRange { lower = lowerRight, upper = upperRight }


compatibleWith : SemanticVersion -> SemanticVersionRange
compatibleWith ver =
    if ver.major < 1 then
        SemanticVersionRange
            { lower = { major = ver.major, minor = ver.minor, patch = 0 }
            , upper = { major = ver.major, minor = ver.minor + 1, patch = 0 }
            }

    else
        SemanticVersionRange
            { lower = { major = ver.major, minor = ver.minor, patch = 0 }
            , upper = { major = ver.major + 1, minor = 0, patch = 0 }
            }


fromExact : SemanticVersion -> SemanticVersionRange
fromExact ({ major, minor, patch } as lower) =
    SemanticVersionRange
        { lower = lower
        , upper = { major = major, minor = minor, patch = patch + 1 }
        }


fromString : String -> Maybe SemanticVersionRange
fromString str =
    when str |> String.keepIf (\char -> char /= ' ') |> String.split "<=v<" is
        [ lowerStr, upperStr ] ->
            when
                { maybeLower = SemanticVersion.fromString lowerStr
                , maybeUpper = SemanticVersion.fromString upperStr
                }
            is
            { maybeLower = Just lower, maybeUpper = Just upper } ->
                Just (SemanticVersionRange { lower = lower, upper = upper })

            _ ->
                Nothing

        _ ->
            Nothing


jsonDecoder : Decoder SemanticVersionRange
jsonDecoder =
    Decode.string
        |> Decode.andThen
            (\strValue ->
                when fromString strValue is
                    Just range ->
                        Decode.succeed range

                    Nothing ->
                        Decode.fail ("Not a valid range: " ++ strValue)
            )    


toString : SemanticVersionRange -> String
toString (SemanticVersionRange { lower, upper }) =
    SemanticVersion.toString lower ++ " <= v < " ++ SemanticVersion.toString upper


toJson : SemanticVersionRange -> Encode.Value
toJson range =
    Encode.string (toString range)
