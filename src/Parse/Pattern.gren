module Parse.Pattern exposing
    ( Error (..)
    , parser
    )


import Parser.Advanced as Parser exposing (Parser)
import AST.Source as AST
import SourcePosition
import Parse.Number as Number
import Parse.String as String
import Parse.Space as Space
import Parse.Variable as Variable


type Error
    = ExpectedChar Char
    | ExpectedKeyword String
    | VariableError Variable.Error
    | NumberError Number.Error
    | StringError String.Error


parser : Parser c Error AST.Pattern
parser =
    Parser.succeed (\start expr end -> SourcePosition.at start end expr)
        |> Parser.keep Parser.getPosition
        |> Parser.keep
            ( Parser.oneOf
                [ Parser.succeed (\name -> AST.PAnything name)
                    |> Parser.skip (Parser.chompIf (\c -> c == '_') (ExpectedChar '_'))
                    |> Parser.keep
                        (Parser.oneOf
                            [ lowerCaseVariable
                            , Parser.succeed ""
                            ]
                        )
                , lowerCaseVariable
                    |> Parser.map AST.PVar
                , Parser.succeed (\start var end -> SourcePosition.at start end var)
                    |> Parser.keep Parser.getPosition
                    |> Parser.keep Variable.foreignUpper
                    |> Parser.keep Parser.getPosition
                    |> Parser.map
                        (\{ start, end, value } ->
                            when value is
                                Variable.Unqualified ctor ->
                                    AST.PCtor
                                        { name = SourcePosition.at start end ctor
                                        , arg = Nothing
                                        }

                                Variable.Qualified { module_, name  } ->
                                    AST.PCtorQual
                                        { varRegion = { start = start, end = end }
                                        , module_ = module_
                                        , name = name
                                        , arg = Nothing
                                        }
                        )
                    |> Parser.mapError VariableError
                ]
            )
        |> Parser.keep Parser.getPosition
        |> Parser.skip Space.parser


lowerCaseVariable : Parser c Error String
lowerCaseVariable =
    Variable.lowerCase
        |> Parser.mapError VariableError
