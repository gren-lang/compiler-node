module Parse.Number exposing
    ( Format (..)
    , Outcome (..)
    , Error (..)
    , parser
    )


import Parser.Advanced as Parser exposing (Parser)


type Format
    = Decimal
    | Hex


type Outcome
    = Integer { format: Format, value : Int }
    | FloatingPoint Float


type Error
    = NotANumber
    | LeadingZero
    | TerminatingDot


parser : Parser c Error Outcome
parser =
    Parser.chompIf (\c -> c == '-' || Char.isDigit c) NotANumber
        |> Parser.skip (Parser.chompWhile Char.isDigit)
        |> Parser.getChompedString
        |> Parser.andThen
            (\str ->
                if String.count str > 1 && (String.startsWith "0" str || String.startsWith "-0" str) then
                    Parser.problem LeadingZero

                else
                    when String.toInt str is
                        Nothing ->
                            Parser.problem NotANumber

                        Just num ->
                            Parser.oneOf
                                [ Parser.chompIf (\c -> c == '.') NotANumber
                                    |> Parser.skip (Parser.chompWhile Char.isDigit)
                                    |> Parser.getChompedString
                                    |> Parser.andThen
                                        (\postDot ->
                                            if postDot == "." then
                                                Parser.problem TerminatingDot

                                            else
                                                when String.toFloat (str ++ postDot) is
                                                    Nothing ->
                                                        Parser.problem NotANumber

                                                    Just float ->
                                                        Parser.succeed (FloatingPoint float)
                                        )
                                , Parser.succeed <|
                                    Integer
                                        { format = Decimal
                                        , value = num
                                        }
                                ]
            )
