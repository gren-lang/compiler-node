module Compiler.Dependencies exposing
    ( SimplifiedOutline
    , Solution(..)
    , solve
    )


{-| The algorithm for calculating the required dependencies of a project.

@docs SimplifiedOutline, Solution, solve
-}

import Compiler.Outline as Outline exposing (Outline)
import Compiler.PackageName as PackageName exposing (PackageName)
import Dict exposing (Dict)
import SemanticVersion exposing (SemanticVersion)
import SemanticVersionRange exposing (SemanticVersionRange)


{-| A simlified outline only contains the information that is required by the algorithm.
-}
type alias SimplifiedOutline =
    { name : PackageName
    , version : SemanticVersionRange
    , dependencies : Dict String SemanticVersionRange
    }


{-| Return value of [solve](#solve). Let's you know if you've got all the required dependencies,
or if you're missing something.
-}
type Solution
    = Complete
    | Missing { name : PackageName, version : SemanticVersionRange }
    | Conflict { name : PackageName, version1 : SemanticVersionRange, version2 : SemanticVersionRange }


{-| Calculate the required dependencies of a project.
-}
solve : Array { name : PackageName, version : SemanticVersionRange } -> Dict String SimplifiedOutline -> Solution
solve rootRequirements loadedOutlines =
    solveHelp rootRequirements Dict.empty loadedOutlines


solveHelp
    : Array { name : PackageName, version : SemanticVersionRange }
    -> Dict String SimplifiedOutline
    -> Dict String SimplifiedOutline
    -> Solution
solveHelp pending solved loaded =
    when Array.popFirst pending is
        Nothing ->
            Complete

        Just { first = { name = packageName, version = packageVersion }, rest } ->
            let
                packageNameStr =
                    PackageName.toString packageName
            in
            when Dict.get packageNameStr solved is
                Just outline ->
                    when SemanticVersionRange.intersect outline.version packageVersion is
                        Nothing ->
                            Conflict
                                { name = outline.name
                                , version1 = outline.version
                                , version2 = packageVersion
                                }

                        Just intersectedVersion ->
                            solveHelp
                                rest
                                (Dict.set packageNameStr { outline | version = intersectedVersion } solved)
                                loaded

                Nothing ->
                    when Dict.get packageNameStr loaded is
                        Nothing ->
                            Missing
                                { name = packageName
                                , version = packageVersion
                                }

                        Just outline ->
                            let
                                newPending =
                                    outline.dependencies
                                        |> Dict.foldl
                                            (\name version acc ->
                                                Array.pushLast
                                                    { name = PackageName.fromString name |> Maybe.withDefault PackageName.example
                                                    , version = version
                                                    }
                                                    acc
                                            )
                                            rest
                            in
                            solveHelp newPending (Dict.set packageNameStr outline solved) loaded
