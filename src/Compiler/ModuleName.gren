module Compiler.ModuleName exposing
    ( ModuleName
    , fromString
    , jsonDecoder
    , toString
    , toJson
    )


{-| A module for working with Gren module names.

@docs ModuleName

## Constructors

@docs fromString, jsonDecoder

## Conversions

@docs toString, toJson
-}

import Json.Decode as Decode exposing (Decoder)
import Json.Encode as Encode


{-|-}
type ModuleName
    = ModuleName String


{-| Construct a [ModuleName](#ModuleName) from a `String`. Will return `Nothing`
if the `String` doesn't represent a valid [ModuleName](#ModuleName).
-}
fromString : String -> Maybe ModuleName
fromString str =
    let
        sections =
            String.split "." str

        sectionTest section =
            when String.popFirst section is
                Just { first, rest } ->
                    Char.isUpper first && String.all Char.isAlphaNum rest

                Nothing ->
                    False
    in
    if Array.all sectionTest sections then
        Just <| ModuleName str

    else
        Nothing


{-| JSON Decoder for [ModuleName](#ModuleName).
-}
jsonDecoder : Decoder ModuleName
jsonDecoder =
    Decode.string
        |> Decode.andThen
            (\str ->
                when fromString str is
                    Just moduleName ->
                        Decode.succeed moduleName

                    Nothing ->
                        Decode.fail ("Not a valid module name: " ++ str)
            )


{-| `String` representation for [ModuleName](#ModuleName).
-}
toString : ModuleName -> String
toString (ModuleName name) =
    name


{-| JSON Encoder for [ModuleName](#ModuleName).
-}
toJson : ModuleName -> Encode.Value
toJson moduleName =
    Encode.string (toString moduleName)
