module Compiler.Paths exposing 
    ( projectRoot
    , projectOutline
    --
    , CacheRoot
    , cacheRoot
    , cacheRootToPath
    , backendBinary
    , packageSources
    , repl
    , exists
    )


{-| Paths commonly used by the compiler.

@docs projectRoot, projectOutline, CacheRoot, cacheRoot, cacheRootToPath, backendBinary, packageSources, repl, exists
-}


import FileSystem
import FileSystem.Path as Path exposing (Path)
import Task exposing (Task)
import Dict exposing (Dict)
import Node
import Compiler.Backend
import SemanticVersion exposing (SemanticVersion)


{-| Search the filesystem for the root of a Gren project, starting with the current working
directory and moving up towards the filesystem root.
-}
projectRoot : FileSystem.Permission -> Task FileSystem.Error Path
projectRoot perm =
    projectOutline perm
        |> Task.map (\grenJsonPath -> Maybe.withDefault Path.empty (Path.parentPath grenJsonPath))


{-| Similar to [projectRoot](#projectRoot), but returns the `Path` to the `gren.json` file of a project.
-}
projectOutline : FileSystem.Permission -> Task FileSystem.Error Path
projectOutline perm =
    FileSystem.currentWorkingDirectory perm
        |> Task.andThen (\workingDir -> projectOutlineHelp perm workingDir)
    

projectOutlineHelp : FileSystem.Permission -> Path -> Task FileSystem.Error Path
projectOutlineHelp perm dir =
    Path.append (Path.fromPosixString "gren.json") dir
        |> FileSystem.checkAccess perm []
        |> Task.onError (\err ->
            if dir /= Path.empty && FileSystem.errorIsNoSuchFileOrDirectory err then
                Path.parentPath dir
                    |> Maybe.withDefault Path.empty
                    |> projectOutlineHelp perm
            
            else
                Task.fail err
        )


{-| A system specific `Path` to a directory where it's common to cache things. The compiler
will use this path to store package sources and the backend binary.
-}
type CacheRoot
    = CacheRoot Path


{-| Construct a [CacheRoot](#CacheRoot) using the current `Platform`, environment variables,
the user's home directory and the current compiler version.
-}
cacheRoot : Node.Platform -> Dict String String -> Path -> SemanticVersion -> CacheRoot
cacheRoot platform envVars homeDir grenVersion =
    let
        startPath =
            when platform is
                Node.Win32 ->
                    envVars
                        |> Dict.get "LOCALAPPDATA"
                        |> Maybe.map Path.fromWin32String
                        |> Maybe.withDefault (
                            "AppData/Local"
                                |> Path.fromPosixString
                                |> Path.prepend homeDir
                            )

                Node.Darwin ->
                    "Library/Caches"
                        |> Path.fromPosixString
                        |> Path.prepend homeDir

                _ ->
                    envVars
                        |> Dict.get "XDG_CACHE_HOME"
                        |> Maybe.map Path.fromPosixString
                        |> Maybe.withDefault (Path.append (Path.fromPosixString ".cache") homeDir)

        
        endPath =
            [ "gren"
            , SemanticVersion.toString grenVersion
            ]
                |> String.join "/"
                |> Path.fromPosixString
    in
    CacheRoot (Path.prepend startPath endPath)


{-| Convert the [CacheRoot](#CacheRoot) to a `Path`.
-}
cacheRootToPath : CacheRoot -> Path
cacheRootToPath (CacheRoot path) =
    path


{-| The `Path` where we'll store the backend binary.
-}
backendBinary : Node.Platform -> CacheRoot -> Path
backendBinary platform (CacheRoot cache) =
    let
        binPath =
            when platform is
                Node.Win32 ->
                    Path.fromPosixString "bin/gren.exe"

                _ ->
                    Path.fromPosixString "bin/gren"
    in
    Path.append binPath cache


{-| The `Path` where we'll store package sources prior to turning them into
`gren_packages` bundles.
-}
packageSources : CacheRoot -> Path
packageSources (CacheRoot cache) =
    Path.prepend cache (Path.fromPosixString "packages")


{-| The `Path` for the project that backs the `gren repl` command.
-}
repl : CacheRoot -> Path
repl (CacheRoot cache) =
    Path.prepend cache (Path.fromPosixString "repl")


{-| Checks to see if the given `Path` points to a real file.
-}
exists : FileSystem.Permission -> Path -> Task x Bool
exists permission path =
    path
        |> FileSystem.checkAccess permission []
        |> Task.map (\_ -> True)
        |> Task.onError (\_ -> Task.succeed False)
