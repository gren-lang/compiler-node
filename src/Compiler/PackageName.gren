module Compiler.PackageName exposing
    ( PackageName
    , example
    --
    , author
    , name
    --
    , fromString
    , cliParser
    , jsonDecoder
    --
    , toString
    , toJson
    )


{-| Functions for working with package identifiers.

@docs Package, author, name, fromString, cliParser, jsonDecoder, toString, toJson
-}


import CLI.Parser
import Json.Encode as Json
import Json.Decode as Decode exposing (Decoder)


{-| An example package name. Useful for tests.
-}
example : PackageName
example =
    PackageName { author = "example", name = "package" }


{-| A package is identified by a string in the following format:

    author/name

Where `author` represents the person, or organization, that built the package, and
`name` is... well, a name. A name should ideally describe the purpose of the package.

Both `author` and `name` can contain alphanumeric characters and a single dash as a seperator,
but no other characters.
-}
type PackageName
    = PackageName 
        { author : String
        , name : String
        }


{-| Retrieve the package author.
-}
author : PackageName -> String
author (PackageName fields) =
    fields.author


{-| Retrieve the package name.
-}
name : PackageName -> String
name (PackageName fields) =
    fields.name


{-| Attempt to convert a `String` into [PackageName](#PackageName).
-}
fromString : String -> Maybe PackageName
fromString str =
    when String.split "/" str is
        [ authorStr, nameStr ] ->
            let
                validAuthor =
                    (not <| String.isEmpty authorStr)
                        && (not <| String.startsWith "-" authorStr)
                        && (not <| String.endsWith "-" authorStr)
                        && (not <| String.contains "--" authorStr)
                        && String.all (\c -> c == '-' || Char.isAlphaNum c) authorStr
                    
                validName =
                    (not <| String.isEmpty nameStr)
                        && (not <| String.startsWith "-" nameStr)
                        && (not <| String.endsWith "-"  nameStr)
                        && (not <| String.contains "--"  nameStr)
                        && String.all (\c -> Char.isDigit c || Char.isLower c || c == '-') nameStr
            in
            if validAuthor && validName then
                when String.popFirst nameStr is
                    Just { first = firstChar } ->
                        if Char.isAlpha firstChar then
                            Just <|
                                PackageName
                                    { author = authorStr
                                    , name = nameStr
                                    }

                        else
                            Nothing

                    Nothing ->
                        Nothing

            else
                Nothing

        _ ->
            Nothing


{-| A parser for use with [CLI.Parser](#CLI.Parser).
-}
cliParser : CLI.Parser.ValueParser PackageName
cliParser =
    { singular = "package"
    , plural = "packages"
    , fn = fromString
    , examples =
        [ "gren-lang/core"
        , "robheghan/string-diff"
        ]
    }


jsonDecoder : Decoder PackageName
jsonDecoder =
    Decode.string
        |> Decode.andThen
            (\strValue ->
                when fromString strValue is
                    Just packageName ->
                        Decode.succeed packageName

                    Nothing ->
                        Decode.fail ("Not a valid package name: " ++ strValue)
            )


{-| Turn [PackageName](#PackageName) into a `String`.
-}
toString : PackageName -> String
toString package =
    author package ++ "/" ++ name package


{-| Turn [PackageName](#PackageName) into `Json`.
-}
toJson: PackageName -> Json.Value
toJson package =
    Json.string <| toString package


